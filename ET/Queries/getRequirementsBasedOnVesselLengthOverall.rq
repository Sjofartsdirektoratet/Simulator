PREFIX : <https://www.sdir.no/SDIR_Simulator#> 
PREFIX scope: <https://www.sdir.no/SDIR_Simulator/shapes/scope#> 
PREFIX unit: <http://qudt.org/vocab/unit/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Return information about a requirement based on specific vessel length overall. 
# Alternative regulation references are only counted, see own query for information retrieval.
# See comments in query for changing magic strings/numbers to variables in some API-call or similar.

SELECT DISTINCT ?paragraph ?scope ?label ?minExclusiveBuiltDate ?maxInclusiveBuiltDate ?tradeArea ?radioArea ?regulationReference ?theme ?subRequirement ?subRequirementLabel ?subRequirementRegulationReference ?subRequirementTheme
WHERE {
  { # find all requirements on all lengths less than input value
    ?paragraph sh:property ?scope .
    ?scope sh:path :vesselLengthOverall ;
           sh:minInclusive ?lengthValue .

    FILTER(?lengthValue <= 8)
  }
  UNION
  { # find all requirements on max lengths larger than input value, not including min lengths larger than input value
    ?paragraph sh:property ?scope .
    ?scope sh:path :vesselLengthOverall ;
       sh:maxExclusive ?lengthValue .
    
    FILTER(?lengthValue > 8)
  }
  
  
  # Return scopes if present, except from vesselType and min/maxLOA
  ?paragraph sh:property ?b .
  
  OPTIONAL { ?b sh:path :builtDate . ?b sh:minExclusive ?minExclusiveBuiltDate . }
  OPTIONAL { ?b sh:path :builtDate . ?b sh:maxInclusive ?maxInclusiveBuiltDate . }
  OPTIONAL { ?b sh:path :tradeArea . ?b sh:hasValue ?tradeArea . }
  OPTIONAL { ?b sh:path :radioArea . ?b sh:hasValue ?radioArea . }

    # Get label in given language
  ?paragraph rdfs:label ?label .
  FILTER(LANG(?label) = 'no') # Insert language tag for label ('no' or 'en')
  
  # Regulation reference URL
  ?paragraph :regulationReference ?regulationReference .
  
  # If present, return information about sub-requirements
  OPTIONAL { 
    ?paragraph :subRequirement ?subRequirement . 
    
    # Get label in given language
    ?subRequirement rdfs:label ?subRequirementLabel .
    FILTER(LANG(?subRequirementLabel) = 'no') # Insert language tag for label ('no' or 'en')

    # Regulation reference URL
    ?subRequirement :regulationReference ?subRequirementRegulationReference .

    # Get tag in given language
    ?subRequirement :theme ?subRequirementTheme .
    FILTER(LANG(?subRequirementTheme) = 'no') # Insert language tag for label ('no' or 'en') 
  }
  
  # Get tag in given language
  ?paragraph :theme ?theme .
  FILTER(LANG(?theme) = 'no') # Insert language tag for label ('no' or 'en') 
  
}

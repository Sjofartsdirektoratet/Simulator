PREFIX : <https://www.sdir.no/SDIR_Simulator#> 
PREFIX scope: <https://www.sdir.no/SDIR_Simulator/shapes/scope#> 
PREFIX unit: <http://qudt.org/vocab/unit/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Return information about a requirement based on specific radio area.
# Including information about sub requirements.
# See comments in query for changing magic strings/numbers to variables in some API-call or similar.

SELECT DISTINCT ?paragraph ?label ?minExclusiveBuiltDate ?maxInclusiveBuiltDate ?tradeArea ?radioArea ?radioAreaLabel ?regulationReference ?theme ?subRequirement ?subRequirementLabel ?subRequirementScope ?subRequirementRegulationReference ?subRequirementTheme
WHERE {
  ?paragraph sh:property ?scope .
  
  # First, check for scope requirement.
  ?scope sh:path :radioArea ;
               sh:hasValue ?radioArea . # Change ?radioArea with any radio area e.g. :A1
  
  ?radioArea rdfs:label ?radioAreaLabel .
  FILTER(LANG(?radioAreaLabel) = 'no') # Insert language tag for label ('no' or 'en')

  # Return scopes if present, except from vesselType and min/maxLOA
  ?paragraph sh:property ?b .
  
  OPTIONAL { ?b sh:path :builtDate . ?b sh:minExclusive ?minExclusiveBuiltDate . }
  OPTIONAL { ?b sh:path :builtDate . ?b sh:maxInclusive ?maxInclusiveBuiltDate . }
  OPTIONAL { ?b sh:path :tradeArea . ?b sh:hasValue ?tradeArea . }
  OPTIONAL { ?b sh:path :radioArea . ?b sh:hasValue ?radioArea . }
  
  # Get label in given language
  ?paragraph rdfs:label ?label .
  FILTER(LANG(?label) = 'no') # Insert language tag for label ('no' or 'en')
  
  # Regulation reference URL
  ?paragraph :regulationReference ?regulationReference .
  
  # If present, return information about sub-requirements
  OPTIONAL { 
    ?paragraph :subRequirement ?subRequirement . 

    # Scope of subRequirement
    ?subRequirement sh:property ?subRequirementScope .
    
    # Get label in given language
    ?subRequirement rdfs:label ?subRequirementLabel .
    FILTER(LANG(?subRequirementLabel) = 'no') # Insert language tag for label ('no' or 'en')

    # Regulation reference URL
    ?subRequirement :regulationReference ?subRequirementRegulationReference .

    # Get tag in given language
    ?subRequirement :theme ?subRequirementTheme .
    FILTER(LANG(?subRequirementTheme) = 'no') # Insert language tag for label ('no' or 'en') 
  }
  
  # Get tag in given language
  ?paragraph :theme ?theme .
  FILTER(LANG(?theme) = 'no') # Insert language tag for label ('no' or 'en') 
}
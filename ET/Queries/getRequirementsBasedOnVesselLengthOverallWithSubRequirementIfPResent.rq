PREFIX : <https://www.sdir.no/SDIR_Simulator#> 
PREFIX scope: <https://www.sdir.no/SDIR_Simulator/shapes/scope#> 
PREFIX unit: <http://qudt.org/vocab/unit/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX sg: <http://www.springernature.com/scigraph/ontologies/core/>

# Return information about a requirement based on specific vessel length overall. 
# Alternative regulation references are only counted, see own query for information retrieval.
# See comments in query for changing magic strings/numbers to variables in some API-call or similar.

SELECT DISTINCT ?paragraph ?label ?scope ?scopeDescription 
				?minExclusiveBuiltDate ?minExclusiveBuiltDateDescription 
				?maxInclusiveBuiltDate ?maxInclusiveBuiltDateDescription 
				?tradeArea ?tradeAreaDescription 
				?radioArea ?radioAreaDescription 
				?regulationReference ?alternativeReference ?eliReference ?theme 
				?subRequirement ?subRequirementLabel ?subRequirementScope ?subRequirementScopeDescription 
				?subRequirementRegulationReference ?subEliReference ?subRequirementTheme
WHERE {
  ?paragraph sh:property ?scope .
  ?scope sh:path :vesselLengthOverall .
  
  { # First, find all minLOA $value
    ?scope sh:minInclusive ?min .
    FILTER(?min = 8) # change magic number to number in question
  }
  UNION
  { # Then, union with all maxLOA > $value
    ?scope sh:maxExclusive ?max .
    FILTER(?max > 8) # change magic number to number in question
  }
  MINUS 
  { # At last, subtract the scopes including a minLOA > $value for handling scopes of ranges (e.g. minLOA_10_maxLOA_15)
    ?scope sh:minInclusive ?minInRange
    FILTER(?minInRange > 8) # change magic number to number in question
  }
  
  # Get label in given language
  ?paragraph rdfs:label ?label .
  FILTER(LANG(?label) = 'no') # Insert language tag for label ('no' or 'en')
  
  ?scope sh:description ?scopeDescription .
  FILTER(LANG(?scopeDescription) = 'no') 

  # Return scopes if present, except from vesselType and min/maxLOA
  ?paragraph sh:property ?b .
  
  OPTIONAL { 
    ?b sh:path :builtDate . 
    ?b sh:minInclusive ?minExclusiveBuiltDate . 
  	?b sh:description ?minExclusiveBuiltDateDescription .
  	FILTER(LANG(?minExclusiveBuiltDateDescription) = 'no') 
  }
  OPTIONAL { 
    ?b sh:path :builtDate . 
    ?b sh:maxExclusive ?maxInclusiveBuiltDate . 
    ?b sh:description ?maxInclusiveBuiltDateDescription .
  	FILTER(LANG(?maxInclusiveBuiltDateDescription) = 'no') 
  }
  OPTIONAL { 
    ?b sh:path :tradeArea . 
    ?b sh:hasValue ?tradeArea . 
    ?b sh:description ?tradeAreaDescription .
  	FILTER(LANG(?tradeAreaDescription) = 'no')
  }
  OPTIONAL { 
    ?b sh:path :radioArea . 
    ?b sh:hasValue ?radioArea .
    ?b sh:description ?radioAreaDescription .
  	FILTER(LANG(?radioAreaDescription) = 'no')
  }
  
  # Regulation reference URL
  ?paragraph :regulationReference ?regulationReference .

  # ELI reference
  ?paragraph :eliReferenceerence ?eliReference .
  
  # Alternative reference URL
  OPTIONAL { ?paragraph :alternativeReference ?alternativeReference . }

  # If present, return information about sub-requirements
  OPTIONAL { 
    ?paragraph :subRequirement ?subRequirement . 
    
    # Scope of subRequirement
    ?subRequirement sh:property ?subRequirementScope .
    ?subRequirementScope sh:description ?subRequirementScopeDescription .
    FILTER(LANG(?subRequirementScopeDescription) = 'no')
    
    # Get label in given language
    ?subRequirement rdfs:label ?subRequirementLabel .
    FILTER(LANG(?subRequirementLabel) = 'no') # Insert language tag for label ('no' or 'en')

    # Regulation reference URL
    ?subRequirement :regulationReference ?subRequirementRegulationReference .

    # ELI reference
    ?subRequirement :eliReferenceerence ?subEliReference .

    # Get tag in given language
    ?subRequirement :theme ?subRequirementTheme .
    FILTER(LANG(?subRequirementTheme) = 'no') # Insert language tag for label ('no' or 'en') 
  }
  
  # Get tag in given language
  ?paragraph :theme ?theme .
  FILTER(LANG(?theme) = 'no') # Insert language tag for label ('no' or 'en')
  
}

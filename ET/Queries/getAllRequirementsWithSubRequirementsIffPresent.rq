PREFIX : <https://www.sdir.no/SDIR_Simulator#> 
PREFIX scope: <https://www.sdir.no/SDIR_Simulator/shapes/scope#> 
PREFIX unit: <http://qudt.org/vocab/unit/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Get all requirements with sub-requriements iff present

SELECT DISTINCT ?paragraph ?label ?scopeDescription 
                ?regulationReference ?alternativeReference ?eliReference ?theme 
                ?subRequirement ?subRequirementLabel ?subRequirementScopeDescription 
                ?subRequirementRegulationReference ?subEliReference ?subRequirementTheme
WHERE {
  ?paragraph a :Requirement .
  
  ?paragraph rdfs:label ?label .
  FILTER(LANG(?label) = 'no') # Insert language tag for label ('no' or 'en')
  
  ?paragraph sh:property ?scope .
  ?scope sh:description ?scopeDescription .
  FILTER(LANG(?scopeDescription) = 'no') 
  
  ?paragraph :regulationReference ?regulationReference .

  ?paragraph :eliReference ?eliReference .
  
  ?paragraph :theme ?theme .
  FILTER(LANG(?theme) = 'no') # Insert language tag for label ('no' or 'en')

  OPTIONAL { ?paragraph :alternativeReference ?alternativeReference . }
    
  ### SUB REQUIREMENT IFF PRESENT
  OPTIONAL {
    ?paragraph :subRequirement ?subRequirement .

    ?subRequirement rdfs:label ?subRequirementLabel .
    FILTER(LANG(?subRequirementLabel) = 'no') # Insert language tag for label ('no' or 'en')

    ?subRequirement sh:property ?subRequirementScope .
    ?subRequirementScope sh:description ?subRequirementScopeDescription .
    FILTER(LANG(?subRequirementScopeDescription) = 'no')

    ?subRequirement :regulationReference ?subRequirementRegulationReference .
    ?subRequirement :eliReference ?subEliReference .

    ?subRequirement :theme ?subRequirementTheme .
    FILTER(LANG(?subRequirementTheme) = 'no') # Insert language tag for label ('no' or 'en')
  }
}
